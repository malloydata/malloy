// Big Value Stories for Storybook
// Tests the # big_value tag for rendering aggregate values as metric cards

source: big_value_test is duckdb.table("static/data/products.parquet") extend {

  // Basic Big Value - multiple aggregates as cards
  #(story)
  # big_value
  view: basic_metrics is {
    aggregate:
      product_count is count()
      # currency
      total_retail is retail_price.sum()
      # currency
      avg_price is retail_price.avg()
  }

  // Big Value with all formatting options
  #(story)
  # big_value
  view: formatted_metrics is {
    aggregate:
      # label="Plain Number"
      product_count is count()

      # label="Currency (USD)"
      # currency
      total_usd is retail_price.sum()

      # label="Custom Format"
      # number="#,##0.0"
      avg_price is retail_price.avg()

      # label="Big Number"
      # number=big
      big_total is retail_price.sum() * 1000

      # label="Profit Margin"
      # percent
      margin is (retail_price.sum() - cost.sum()) / retail_price.sum()
  }

  // Big Value with size=lg
  #(story)
  # big_value { size=lg }
  view: large_metrics is {
    aggregate:
      # currency
      total_retail is retail_price.sum()
      # currency
      avg_price is retail_price.avg()
  }

  // Big Value with size=sm
  #(story)
  # big_value { size=sm }
  view: small_metrics is {
    aggregate:
      product_count is count()
      # currency
      total_retail is retail_price.sum()
  }

  // Big Value with comparison - percentage change
  #(story)
  # big_value
  view: with_comparison_pct is {
    aggregate:
      # label="Current Sales"
      # currency
      current_sales is retail_price.sum()

      # big_value { comparison_field='current_sales' comparison_label='vs Prior Period' }
      # currency
      prior_sales is retail_price.sum() * 0.85
  }

  // Big Value with down_is_good (for cost metrics)
  #(story)
  # big_value
  view: down_is_good_example is {
    aggregate:
      # label="Current Costs"
      # currency
      current_costs is cost.sum()

      # big_value { comparison_field='current_costs' comparison_label='vs Budget' down_is_good='true' }
      # currency
      budgeted_costs is cost.sum() * 1.10
  }
}
