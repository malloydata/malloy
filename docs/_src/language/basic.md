# Malloy Quickstart

This guide introduces the basics of querying and modeling with Malloy. Malloy has a rendering system that can render [results as tables, charts or dashboards](../visualizations/dashboards.md), but fundamentally the Malloy just returns data. Buttons on the top right of any "Query Result" box allow you to toggle between rendered results (HTML), raw data (JSON), and the SQL generated by the malloy model.

If you'd like to follow along with this guide, you can create a new <code>.malloy</code> file and run these queries there.



## Leading with the Source

Queries are of the form: _source_ `->` _operation_

In Malloy, the source of a query is either a raw table, a [modeled source](source.md), or another query.

In this example, the `table()` function provides the query _source_ from a table (or view) in the database.
The query _operation_ is explicit about which fields are grouped, aggregated or projected.


```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  group_by: state
  aggregate: airport_count is count()
}
```


## Query Operators

In SQL, the <code>SELECT</code> command does two very different things.  A <code>SELECT</code> with a <code>GROUP BY</code> aggregates data according to the <code>GROUP BY</code> clause and produces aggregate calculation against every calculation not in the <code>GROUP BY</code>.  In Malloy, the query operator for this is `group_by:`.  Calculation about data in the group are made using `aggregate:`.

The second type of <code>SELECT</code> in SQL does not perform any aggregation;  All rows in the input table, unless filtered in some way, show up in the output table. In Malloy, this command is called `project:`.

In the query below, the data will be grouped by `state` and will produce an aggregate calculation for `airport_count` and `average_elevation`.  `group_by:`. The `aggregate:` list can contain references to existing aggregate fields or add new aggregate computations.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  group_by: [
    state
    county
  ]
  aggregate: [
    airport_count is count()
    average_elevation is avg(elevation)
  ]
}
```

### Multiple Field Operations

Multiple `group_by:` and `aggregate:` statements can appear in the same query operation.  This can be helpful in rendering when the order of fields in the query output is significant.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  group_by: fac_type
  aggregate: airport_count is count()
  group_by: county
  aggregate: max_elevation is max(elevation)
}
```

### Project
`project` produces a list of fields.  For every row in the input table, there is a row in the output table.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  project: [ code, full_name, city, county ]
  where: county = 'SANTA CRUZ'
}
```

## Everything has a Name

In Malloy, all output fields have names. This means that any time a query
introduces a new aggregate computation, it must be named. _(unlike SQL,
which allows un-named expressions)_

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  aggregate: max_elevation is max(elevation)
}
```

Notice that Malloy uses the form "_name_ `is` _value_" instead of SQL's "_value_ `as` _name_".
Having the output column name written first makes it easier for someone reading
the code to visualize the resulting query structure.

Named objects, like columns from a table, and fields defined in a source, can be included in field lists without an `is`

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  project: [
    full_name
    elevation
  ]
}
```

## Expressions

Many SQL expressions will work unchanged in Malloy, and many functions available in Standard SQL are usable in Malloy as well. This makes expressions fairly straightforward to understand, given a knowledge of SQL.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true, "size": "large"}
query: table('malloy-data.faa.airports') -> {
  group_by: county_and_state is concat(county, ', ', state)
  aggregate: [
    airport_count is count()
    max_elevation is max(elevation)
    min_elevation is min(elevation)
    avg_elevation is avg(elevation)
  ]
}
```

The basic types of Malloy expressions are `string`, `number`, `boolean`, `date`, and `timestamp`.

## Modeling and Reuse

One of the main benefits of Malloy is the ability to save common calculations into a data model.  In the example below, we create a *source* object named `airports` and add a `dimension:` calculation for `county_and_state` and `measure:` calculation for `airport_count`.  Dimensions can be used in `group_by:`, `project:` and `where:`.  Measures can be used in `aggregate:` and `having:`.

```malloy
--! {"isModel": true, "modelPath": "/inline/airports_mini.malloy"}
source: airports is table('malloy-data.faa.airports') {
  dimension: county_and_state is concat(county, ', ', state)
  measure: airport_count is count()
  measure: average_elevation is avg(elevation)
}
```

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true, "source": "/inline/airports_mini.malloy"}
query: airports -> {
  group_by: county_and_state
  aggregate: airport_count
}
```

## Ordering and Limiting

In Malloy, ordering and limiting work pretty much the same way they do in SQL, though Malloy introduces some [reasonable defaults](order_by.md).

The `top:` and `limit:` statements are synonyms and limits the number of rows returned. Results below are sorted by the first measure descending--in this case, `airport_count`.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  top: 2
  group_by: state
  aggregate: airport_count is count()
}
```

Default ordering can be overridden with `order_by:`, as in the following query, which shows the states in alphabetical order.  `order_by:` can take a field index number or the name of a field.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  order_by: state
  group_by: state
  aggregate: airport_count is count()
}
```

## Filtering

When working with data, filtering is something you do in almost every query. Malloy's filtering is more powerful and expressive than that of SQL. When querying data, we first isolate the data we are interested in (filter it) and then perform aggregations and calculations on the data we've isolated (shape it). Malloy provides consistent syntax for filtering everywhere within a query.

### Filtering the Source

A filter on a data source table narrows down which data is included to be passed to the query _operation_. This translates
to a <code>WHERE</code> clause in SQL.
In this case, the data from the table is filtered to just airports in California.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') { where: state = 'CA' } -> {
  top: 2
  group_by: county
  aggregate: airport_count is count()
}
```

### Filtering Query Stages

Filters can also be applied to any query _operation_. When using a filter in this way, it only applies to
the data for that operation alone. (More on this later, in the section on `nest:` operations in queries.)

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  where: state: 'AL' | 'KY'
  top: 5
  group_by: state
  aggregate: [
    airports  is count() { where: fac_type: 'AIRPORT' }
    heliports is count() { where: fac_type: 'HELIPORT' }
    total     is count()
  ]
}
```

A note on filtering the source vs filtering in query stages: The below queries are both valid and produce identical SQL.

```malloy
query: table('malloy-data.faa.airports') { where: state = 'CA' } -> {
  top: 2
  group_by: county
  aggregate: airport_count is count()
}

query: table('malloy-data.faa.airports') -> {
  where: state = 'CA'
  top: 2
  group_by: county
  aggregate: airport_count is count()
}
```


### Filtering Measures

A filter on an aggregate calculation (a _measure_) narrows down the data used in that specific calculation. In the example below, the calculations for `airports` and `heliports` are filtered separately.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
query: table('malloy-data.faa.airports') -> {
  group_by: state
  aggregate: [
    airports is count() { where: fac_type = 'AIRPORT' }
    heliports is count() { where: fac_type = 'HELIPORT' }
    total is count()
  ]
}
```

## Dates and Timestamps

Working with time in data is often needlessly complex; Malloy has built in constructs to simplify many time-related operations. This section gives a brief introduction to some of these tools, but for more details see the [Time Ranges](time-ranges.md) section.

### Time Literals

Literals of type `date` and `timestamp` are notated with an `@`, e.g. `@2003-03-29` or `@1994-07-14 10:23:59`. Similarly, years (`@2021`), quarters (`@2020-Q1`), months (`@2019-03`), weeks (`@WK2021-08-01`), and minutes (`@2017-01-01 10:53`) can be expressed.

Time literals can be used as values, but are more often useful in filters. For example, the following query
shows the number of flights in 2003.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto"}
query: table('malloy-data.faa.flights') { where: dep_time: @2003 } -> {
  aggregate: flight_count is count()
}
```

There is a special time literal `now`, referring to the current timestamp, which allows for relative time filters.

```malloy
query: table('malloy-data.faa.flights') { where: dep_time > now - 6 hours } -> {
  aggregate: flights_last_6_hours is count()
}
```

### Truncation

Time values can be truncated to a given timeframe, which can be `second`, `minute`, `hour`, `day`, `week`, `month`, `quarter`, or `year`.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto"}
query: table('malloy-data.faa.flights') -> {
  group_by: [
    flight_year is dep_time.year
    flight_month is dep_time.month
  ]
  aggregate: flight_count is count()
}
```

### Extraction

Numeric values can be extracted from time values, e.g. `day_of_year(some_date)` or `minute(some_time)`. See the full list of extraction functions [here](time-ranges.md#extraction).

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "pageSize": 7, "size": "large"}
query: table('malloy-data.faa.flights') -> {
  order_by: 1
  group_by: day_of_week is day(dep_time)
  aggregate: flight_count is count()
}
```

<!-- TODO it may be worth having a doc describing what the JSON+Metadata
output of these look like, i.e. that the JSON just includes a regular date,
but the metadata specifies that it's in that given timeframe.
And likewise for any other data type that has interesting output metadata. -->

### Time Ranges

Two kinds of time ranges are given special syntax: the range between two times and the range starting at some time for some duration. These are represented like `@2003 to @2005` and `@2004-Q1 for 6 quarters` respectively. These ranges can be used in filters just like time literals.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto"}
query: table('malloy-data.faa.flights') { where: dep_time: @2003 to @2005 } -> {
  aggregate: flight_count is count()
}
```

Time literals and truncations can also behave like time ranges. Each kind of time literal has an implied duration that takes effect when it is used in a comparison, e.g. `@2003` represents the whole of the year 2003, and `@2004-Q1` lasts the whole 3 months of the quarter. Similarly, when a time value is truncated, it takes on the
timeframe from the truncation, e.g. `now.month` means the entirety of the current month.

When a time range is used in a comparison, `=` checks for "is in the range", `>` "is after", and `<` "is before." So `some_time > @2003` filters dates starting on January 1, 2004, while `some_time = @2003` filters to dates in the year 2003.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto"}
query: table('malloy-data.faa.flights') { where: dep_time > @2003 } -> {
  top: 3; order_by: departure_date asc
  group_by: departure_date is dep_time.day
  aggregate: flight_count is count()
}
```

## Nested Queries

The next several examples will use this simple source definition:

```malloy
source: airports is table('malloy-data.faa.airports') {
  airport_count is count()
};
```

### Aggregating Subqueries

In Malloy, queries can be [nested](nesting.md) to produce subtables on each output row. Such nested queries are called _aggregating subqueries_, or simply "nested queries." When a query is nested inside another query, each output row of the outer query will have a nested table for the inner query which only includes data limited to that row.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "source": "faa/airports.malloy"}

query: airports -> {
  group_by: state
  aggregate: airport_count
  nest: by_facility is {
    group_by: fac_type
    aggregate: airport_count
  }
}
```

Here we can see that the `by_facility` column of the output table contains nested subtables on each row. When interpreting these inner tables, all of the dimensional values from outer rows still apply to each of the inner rows.

Queries can be nested infinitely, allowing for rich, complex output structures. A query may always include another nested query, regardless of depth.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "source": "faa/airports.malloy", "size": "large"}
query: airports -> {
  group_by: state
  aggregate: airport_count
  nest: top_5_counties is {
    top: 5
    group_by: county
    aggregate: airport_count
    nest: by_facility is {
      group_by: fac_type
      aggregate: airport_count
    }
  }
}
```

### Filtering Nested Queries

Filters can be isolated to any level of nesting. In the following example, we limit the `major_facilities` query to only airports where `major` is `'Y'`. This particular filter applies _only_ to `major_facilities`, and not to other parts of the outer query.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "source": "faa/airports.malloy", "size": "large"}
query: airports -> {
  where: state = 'CA'
  group_by: county
  aggregate: airport_count
  nest: major_facilities is {
    where: major = 'Y'
    group_by: name is concat(code, ' (', full_name, ')')
  }
  nest: by_facility is {
    group_by: fac_type
    aggregate: airport_count
  }
}
```

## Pipelines and Multi-stage Queries

The output from one stage of a query can be passed into another stage using `->`. For example, we'll start with this query which outputs, for California and New York, the total number of airports, as well as the number of airports in each county.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "source": "faa/airports.malloy", "size": "small"}
query: airports -> {
  where: state = 'CA' | 'NY'
  group_by: state
  aggregate: airport_count
  nest: by_county is {
    group_by: county
    aggregate: airport_count
  }
}
```

Next, we'll use the output of that query as the input to another, where we determine which counties have the highest
percentage of airports compared to the whole state, taking advantage of the nested structure of the data to to so.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "source": "faa/airports.malloy", "size": "large", "dataStyles": { "percent_in_county": { "renderer": "percent" }}}
query: airports -> {
  where: state = 'CA' | 'NY'
  group_by: state
  aggregate: airport_count
  nest: by_county is {
    group_by: county
    aggregate: airport_count
  }
} -> {
  top: 10; order_by: 4 desc
  project: [
    by_county.county
    airports_in_county is by_county.airport_count
    airports_in_state is airport_count
    percent_in_county is by_county.airport_count / airport_count
  ]
}
```

## Joins

[Joins](join.md) are declared as part of a source. When joining a source to another, it brings with it all child joins.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "isPaginationEnabled": true}
source: aircraft_models is table('malloy-data.faa.aircraft_models') {
  primary_key: aircraft_model_code
}
source: aircraft is table('malloy-data.faa.aircraft') {
  primary_key: tail_num
  join_one: aircraft_models on aircraft_model_code = aircraft_models.aircraft_model_code
}
source: flights is table('malloy-data.faa.flights') {
  join_one: aircraft on tail_num = aircraft.tail_num
}
query: flights->{
  where: dep_time: @2003-01
  group_by: aircraft.aircraft_models.manufacturer
  aggregate: [
    flight_count is count()
    aircraft_count is aircraft.count()
    average_seats_per_model is aircraft.aircraft_models.seats.avg()
  ]
}
```

In this example, the `aircraft` source is joined to `flights`, and aircraft_models is joined via aircraft. These examples explicitly name both keys--this same syntax can be used to write more complex joins.

## Aggregate Calculations

As in SQL, aggregate functions `sum`, `count`, and `avg` are available, and their use in
an expression identifies the corresponding field as a [measure](fields.md#measures).

Aggregates may be computed with respect to any joined source, allowing for a wider variety of measurements to be calculated than is possible in SQL. See the [Aggregate Locality](aggregates.md#aggregate-locality) section for more information.

```malloy
--! {"isRunnable": true, "showAs":"json", "runMode": "auto", "source": "faa/flights.malloy"}
query: aircraft -> {
  aggregate: [
    // The average number of seats on models of registered aircraft
    models_avg_seats is aircraft_models.seats.avg()
    // The average number of seats on registered aircraft
    aircraft_avg_seats is avg(aircraft_models.seats)
  ]
}
```

## Comments

Malloy code can include both line and block comments. Line comments, which begin with `--` or `//`,
may appear anywhere within a line, and cause all subsequent characters on that line to be ignored.
Block comments, which are enclosed between <code>/\*</code> and <code>\*/</code>, cause all enclosed characters to be ignored
and may span multiple lines.

```malloy
-- The total number of flight entries
query: flights -> {
  aggregate: flight_count // Defined simply as `count()`
}

/*
 * A comparison of the total number of flights
 * for each of the tracked carriers.
 */
query: flights -> {
  group_by: carrier
  aggregate: [ flight_count /* , total_distance */ ]
}
```




<!-- ## Joins are between primary and foreign keys.


## Full graph of the data is available to query

## Sums and Counts and average are a little different.

## Calculations can correctly occur anywhere in the graph -->



<!--

## Removed things
- Commas are optional.
- Count can be written without the `*`.

-->
