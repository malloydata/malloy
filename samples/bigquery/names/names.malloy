--! styles names.styles.json

source: names is table('bigquery-public-data.usa_names.usa_1910_current') {

  dimension: decade is floor(`year` / 10) * 10

  measure:
    total_population is `number`.sum()
    births_per_100k is floor(total_population / all(total_population) * 100000)

  query: by_name is {
    group_by: name
    aggregate: total_population, births_per_100k
  }

  query: state_map is {
    group_by: state
    aggregate: births_per_100k
  }

  query: decade_graph is {
    group_by: decade
    aggregate: births_per_100k
    group_by: gender
    order_by: decade asc
  }

  query: name_dashboard is {
    top: 15
    group_by: name
    aggregate: total_population
    nest: state_map, decade_graph
  }

  query: location_iconic_names is {
    group_by: name, gender
    aggregate: births_per_100k
    nest: by_state is { group_by: state; aggregate: births_per_100k }
  } -> {
    where: births_per_100k > 50
    group_by: by_state.state
    nest: by_gender is {
      group_by: gender
      nest: iconic_name_list is {
        top: 15; order_by: 2 desc
        group_by: name
        aggregate: popularity is (by_state.births_per_100k - births_per_100k) / births_per_100k
      }
    }
  }

  query: time_iconic_names is {
    group_by: name, gender
    aggregate: births_per_100k
    nest: by_decade is {
      group_by: decade
      aggregate: births_per_100k
    }
  } -> {
    where: births_per_100k > 50
    group_by: by_decade.decade
    nest: by_gender is {
      group_by: gender
      nest: iconic_name_list is {
        top: 50; order_by: 2 desc
        group_by: name
        aggregate: popularity is (by_decade.births_per_100k - births_per_100k) / births_per_100k
      }
    }
  }
}

query:  jes_dashboard is names -> name_dashboard { where: name ~ r'Jes' }

query: jessie_over_time is names -> {
  where: name = 'Jessie'
  nest: decade_graph
  nest: total_population
  nest: by_state_and_time is {
    group_by: state
    aggregate: total_population
    nest: decade_graph
  }
}

query: alabama_iconic_names_by_decade is names -> time_iconic_names { where: state ? 'AL' }

query: newyork_iconic_names_by_decade is names -> time_iconic_names { where: state ? 'NY' }

query: old_iconic_names_by_state is names -> location_iconic_names { where: decade <= 1940 }
