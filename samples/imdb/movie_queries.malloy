import "imdb.malloy"


source: movies_queries is movies + {

  // Queries...  

  // Top Movies by numVotes (total number of people that liked it)
  query: top20 is by_title + { limit: 20 } 

  // Movie Stars
  query:  top_star is by_name + { 
    limit: 20 
  } 

  // Movie Stars and jobs
  query: top_stars_andjobs is  by_name + { 
    nest: by_job_category
    limit: 20 
  } 

  // Movie Stars Titles time and jobs
  query: top_stars_jobs_and_more_dashboard is by_name + { 
    nest: 
      by_title + { limit: 5}
      by_year_line_chart is by_year
      by_job_category
    limit: 20 
  } 

  // top Directors and their 5 best movies
  query: top_directors is by_name + {
    where: principals.category = 'director'
    nest: by_title + { limit: 5}
  }

  // top Comedy Directors and their 5 best movies
  query: top_comedy_directors is  by_name + {
    where: principals.category = 'director', genres.value = 'Comedy'
    nest: by_title + { limit: 5}
  }

  // by Genre, top movies, directors and actors
  query: genres_top_movies_directors_and_actors is  by_genre + {
    nest: by_title + { limit: 5}
    nest: by_director is by_name + {
      where: principals.category = 'director'
      limit: 5
    }
    nest: by_actor is by_name + {
      where: principals.category ~ 'act%'
      limit: 5
    }
  }
}
