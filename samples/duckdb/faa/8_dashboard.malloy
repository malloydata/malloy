import "2_flights.malloy"

// Dashboard
//  Runs as a single query, nesting all the results
//  View the results as JSON to see the data returned.
//  
//  The last part of the name controls the rendering.

// see:
// dashboards: 
//  https://looker-open-source.github.io/malloy/documentation/visualizations/dashboards.html
// nesting:
//  https://looker-open-source.github.io/malloy/documentation/language/nesting.html
//

source: flights_dashboarding is flights {
 
  query: carrier_dashboard is { //# dashboard
    aggregate: destination_count is destination.airport_count
    aggregate: flight_count
    nest: by_manufacturer is {
      group_by: aircraft.aircraft_models.manufacturer
      aggregate: flight_count, aircraft.aircraft_count
    }
    nest: by_month_line_chart is {         //# line_chart
      group_by: dep_month is dep_time.month
      aggregate: flight_count
    }
    nest: hubs is {
      top: 10
      where: destination.airport_count > 1
      group_by: hub is origin.name
      aggregate: destination_count is destination.airport_count
    }
    nest: origin_dashboard is {   //# dashboard
      top: 10
      group_by:
        code is origin_code,
        origin is origin.full_name,
        origin.city
      aggregate: flight_count
      nest: destinations_by_month_line_chart is {
        group_by: dep_month is dep_time.month
        aggregate: flight_count
        group_by: destination.name
      }
      nest: routes_map_segment_map is {
        group_by:
          origin.latitude
          origin.longitude
          latitude2 is destination.latitude
          longitude2 is destination.longitude
        aggregate: flight_count
      }
    }
  }
  query: southwest_dashboard is carrier_dashboard {
    where: carriers.nickname ? 'Southwest'
  }
  query: jetblue_dashboard is carrier_dashboard {
    where: carriers.nickname ? 'Jetblue'
  }

}