import "1_flights.malloy"

// Sessionization - 
//  This is a subset of the data.
// 
//  If we think of aircraft flights as a transaction.  We can roll up all transactions
//  in a given day by a carrier into a single nested table.
//
//  In the result you can see that Delta had 13 flights on 03-03 and the individual
//    aircraft events (ordered through the day)
//
//  see: https://looker-open-source.github.io/malloy/documentation/patterns/foreign_sums.html
//  

source: flights_sessions is flights {
 
  query: sessionize is {
    where: carrier ? 'WN' | 'DL', dep_time ? @2002-03-03 

    group_by: flight_date is dep_time.day
    group_by: carrier
    aggregate: 
      daily_flight_count is flight_count
      aircraft.aircraft_count
    nest: per_plane_data is {
      top: 20
      group_by: tail_num
      aggregate: plane_flight_count is flight_count
      nest: flight_legs is {
        order_by: 2
        group_by:
          tail_num
          dep_minute is dep_time.minute
          origin_code
          dest_code is destination_code
          dep_delay
          arr_delay
      }
    }
  }
}