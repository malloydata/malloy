import "2_flights.malloy"

// Time based Analysis
//  

source: flights_carrier is flights {

  // make some data
  query: carriers_over_time is {
    group_by: dep_month is dep_time.month
    aggregate: flight_count
    group_by: carriers.nickname
    limit: 10000
  }

  // show it as a line chart
  //  see: https://looker-open-source.github.io/malloy/documentation/visualizations/charts_line_chart.html
  query: carriers_over_time_line_chart is carriers_over_time


  // nesting result
  //  see: https://looker-open-source.github.io/malloy/documentation/language/nesting.html
  query: by_destination is {
    group_by: destination.name
    aggregate: flight_count
    nest: carriers_over_time_line_chart is carriers_over_time
  }

  query: chicago_over_time is {
    where: origin.code = 'ORD'
    group_by: destination.name
    aggregate: flight_count
    nest: carriers_over_time_line_chart is carriers_over_time
    limit: 50
  }

  // compare timeframes using filtered measures:
  //  
  query: by_destination_growth_rate is {
    declare: 
      flight2004 is flight_count {? dep_time: @2004}  // can use relative declarations
      flight2003 is flight_count {? dep_time: @2003}  //  also
    group_by: destination.name
    aggregate: 
      flight2004
      flight2003
      delta is flight2004 - flight2003
      growth is (flight2004 - flight2003)/flight2004*100
  }

}