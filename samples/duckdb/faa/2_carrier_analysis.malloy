
// carrier analysis.
//  show where carrires fly, what kinds of planes they use
// 
// overview of carriers
//   What percentage of the market does a carrier have?
//   Where are their hubs and what percatage of flights go through that hub?
//   At various hubs, what percentage of flights are made by that carrier.


// import existing source 'flights' from flights.malloy
//  see: https://looker-open-source.github.io/malloy/documentation/language/imports.html
//
import "1_flights.malloy"


// source 'flights_carrier' refines source 'flights' 
//  https://looker-open-source.github.io/malloy/documentation/language/source.html#source-refinement
source: flights_carrier is flights + {

    // 
  // flight_count - aggregate calculation count(*), imported from
  //  see: 
  //    https://looker-open-source.github.io/malloy/documentation/language/aggregates.html
  //
  // all(flight_count) - computes flight_count without any group_by
  //
  // exclude(flight, carrier_name) - computes flight_count with the current 
  //   group_by except carrier_name
  query: carrier_overview is {
    group_by: carrier_name is carriers.nickname
    aggregate:
      flight_count
      percent_of_this_carriers_flights_to_all_flights
        is flight_count/all(flight_count)*100
    nest: top_destinations is {
      group_by: destination.code, destination.full_name
      aggregate:
        flight_count
        flights_to_dest is exclude(flight_count, carrier_name)
        percentage_of_this_carriers_flights_to_this_destination
          is flight_count/all(flight_count)*100
        percentage_of_all_flights_on_all_carriers_to_this_destination
          is flight_count/exclude(flight_count, carrier_name)*100
      limit: 10
    }
  }
}
