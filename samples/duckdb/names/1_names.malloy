//
// Basic names analysis.
//
//  Table has 5 columns name, state, year, gender and number (population)
//
//  number of people in that state, year, gender born with that name

//
// Malloy language Documentation
//  see: https://malloydata.github.io/malloy/documentation/
//

// source, see: https://malloydata.github.io/malloy/documentation/language/source.html
source: names is table('duckdb:data/usa_names.parquet') {


  measure: population is `number`.sum()
  dimension: decade is floor(`year`/10)*10


  // query, see: https://malloydata.github.io/malloy/documentation/language/query.html
  query: by_name is {
    group_by: name
    aggregate: population
    limit: 10
  }

  query: by_state is {
    group_by: state
    aggregate: population
  }

  query: by_gender is {
    group_by: gender
    aggregate: population
  }

  query: by_year is {
    group_by: `year`
    aggregate: population
    order_by: 1 asc
  }

  query: by_decade is {
    group_by: decade
    aggregate: population
    order_by: 1 asc
  }

  // filters, see: https://malloydata.github.io/malloy/documentation/language/filters.html
  query: male_names is by_name {? gender = 'M'}
  query: female_names is by_name {? gender = 'F'}

  query: top_names_by_state_ea_gender is by_state {
    nest: male_names
    nest: female_names
    limit: 10
  }

  query: name_dashboard is by_name {
    nest:
      by_decade
      by_state
    limit: 10
  }
}

// named querys
//  see: https://malloydata.github.io/malloy/documentation/language/query.html
query: j_names is names -> name_dashboard {? name ~ r'J'}
