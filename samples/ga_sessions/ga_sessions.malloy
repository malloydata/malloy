--! styles ga_sessions.styles.json

explore:ga_sessions is table('bigquery-public-data.google_analytics_sample.ga_sessions_20170801'){
  dimension: start_time is cast(timestamp_seconds(visitStartTime) as timestamp)

  measure: [
    user_count is count(distinct fullVisitorId)
    session_count is count(*)
    total_visits is totals.visits.sum()
    total_hits is totals.hits.sum()
    total_page_views is totals.pageviews.sum()
    t2 is totals.pageviews.sum()
    total_productRevenue is hits.product.productRevenue.sum()
    hits_count is hits.count()
    sold_count is hits.count() {where: hits.product.productQuantity >0 }
  ]

  query: by_source is {
    where: trafficSource.source != '(direct)'
    group_by: trafficSource.source
    aggregate: hits_count
    limit: 10
  }
  query: by_adContent_bar_chart is {
    group_by: device.browser
    aggregate: user_count
    group_by: device.deviceCategory
  }
  query: by_region is {
    where: geoNetwork.region !~ '%demo%'
    group_by: geoNetwork.region
    aggregate: user_count
    limit: 10
  }
  query: by_device is {
    group_by: device.browser
    aggregate: user_count
    group_by: device.deviceCategory
  }
  query: by_category is {
    group_by: category is hits.product.v2ProductCategory
    aggregate: total_productRevenue
    aggregate: sold_count
    limit: 10
  }
  query: by_hour_of_day is {
    group_by: gsession_hour is hour(start_time)
    aggregate: session_count
    order_by: 1
  }
  query: page_load_times is {
    group_by: hits.page.pageTitle
    aggregate: hit_count is hits.count()
    nest: load_bar_chart is {
       group_by: hit_seconds is floor(hits.latencyTracking.pageLoadTime/2)*2
       aggregate: hits_count
    }
    limit: 10
  }

  query: by_page_title is { where: totals.transactionRevenue > 0
    group_by: hits.page.pageTitle
    aggregate: hits_count
    nest: sold_count
  }

  query: by_all is {
    nest: by_source
    nest: by_adContent_bar_chart
    nest: by_region
    nest: by_category
  }
}

query: sessions_dashboard is ga_sessions -> {
  nest: [
    by_region
    , by_device
    , by_source
    , by_category is {
        group_by: category is hits.product.v2ProductCategory
        aggregate: total_productRevenue
        aggregate: sold_count
    }
  ]
}