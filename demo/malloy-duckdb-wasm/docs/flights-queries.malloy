import "./flights.malloy"

// --
// Name: Carrier Table
query: carriers -> {
  project: *
}

// --
// Name: Flights Table
query: flights -> {
  project: *
}

// --
// Name: Named query
query: flights -> by_carrier

// --
// Name: Named query with filter: SFO carriers
query: flights {
  where: orig.code = 'SFO'
} -> by_carrier

// --
// Name: Named Query: By Origin
query: flights -> by_origin

// --
// Name: Named Query: By Origin with Top 3 Carrier Nested
query: flights-> by_origin + {
  nest: by_carrier +  {limit: 3}
  limit: 30
}

// --
// Name: Named Query: By Carrier with Top 3 Origins Nested
query: flights-> by_carrier + {
  nest: by_origin +  {limit: 3}
  limit: 30
}

// --
// Name: Simple Barchart: Carriers by Flights
query: flights->  {
  nest: by_carrier_bar_chart is {
    group_by: carriers.nickname
    aggregate: flight_count
  }
}

// --
// Name: Barchart with two measures: Carriers by Flights
query: flights->  {
  group_by: orig.faa_region
  aggregate: flight_count
  nest: by_carrier_bar_chart is {
    group_by: carriers.nickname
    aggregate:
      flight_count
      dest_count
  }
}

// --
// Name: Barchart with two dimensions: Carriers by Flights and Distance
query: flights->  {
  nest: by_carrier_bar_chart is {
    group_by: carriers.nickname
    aggregate: flight_count
    group_by: flight_length
  }
}

// --
// Name: Line Chart Simple: flights by month
query: flights->  {
  group_by: carriers.nickname
  nest: by_month_line_chart is {
    group_by: dep_qtr is dep_time.quarter
    aggregate: flight_count
  }
}

// --
// Name: Line Chart with two dimension: Flights by Month and Length
query: flights->  {
  group_by: carriers.nickname
  nest: by_month_line_chart is {
    group_by: dep_qtr is dep_time.quarter
    aggregate: flight_count
    group_by: flight_length
  }
}

// --
// Name: Line Chart with two dimension: Flights by Month and Length
query: flights->  {
  group_by: carriers.nickname
  nest: by_month_line_chart is {
    group_by: dep_qtr is dep_time.quarter
    aggregate: flight_count
    group_by: flight_length
  }
}

// --
// Name: Lists: Origins and the list of carriers
query: flights->  {
  group_by: orig.code, orig.city
  nest: carrier_list is {
    group_by: carriers.nickname
    aggregate: flight_count
  }
  limit: 300
}

// --
// Name: List Detail: Origins and the list of carriers
// shows the count for each member of the list
query: flights->  {
  group_by: orig.code, orig.city
  nest: carrier_list_detail is {
    group_by: carriers.nickname
    aggregate: flight_count
  }
  limit: 300
}


// --
// Name: Shape Map: Carriers Origin by State
query: flights->  {
  group_by: carriers.nickname
  aggregate: flight_count
  nest: by_state_shape_map is {
    group_by: orig.state
    aggregate:
      flight_count
      dest_count
  }
}

// --
// Name: Top N of a Single Dimension
// It is often interesting to pull the topN of a
//  particular dimension.  Using pipelines
//  we can run two queries simultaneously and
//  join the results.

source: my_flights is flights + {
  query: top_carrier_line_chart is {
      nest: c is {
        group_by: qtr is dep_time.quarter
        aggregate: flight_count
        group_by: carriers.nickname
      }
      nest: top_carriers is {
          group_by: carriers.nickname
          aggregate: flight_count
          top: 5
      }
  }
  -> {
    project: c.*
    where: c.nickname = top_carriers.nickname
  }

  query: origin_top_carriers is {
    group_by: origin, orig.city
    aggregate: flight_count
    nest: top_carrier_line_chart
  }
}
query: my_flights->origin_top_carriers


// --
// Name: Using one query as a filter for another
// Filter by carriers that fly to SFO.
query: sfo_carriers is flights-> {
  where: orig.code = 'SFO'
  group_by: carrier
}

query: flights -> {
  join_one: sfo is from(->sfo_carriers) on carrier = sfo.carrier
  where: sfo.carrier != null
  group_by: carriers.name
  aggregate: flight_count, origin_count is orig.airport_count
}