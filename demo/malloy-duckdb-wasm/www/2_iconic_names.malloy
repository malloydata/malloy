  // the most iconic names in each state or decade.
  //  popularity is computed by taking a ratio of how prevelent the
  //  name is in a given state and dividing it by its over all popularity
  //  in the data set

import "1_names.malloy"

source: names_iconic is names + {
  
  // the 'exclude' function computes the aggregate calculation
  //  but computes it as if the list of dimensions were not used
  //  in the query
  measure: 
    total_name_populution is exclude(population, state)
    state_population is exclude(population, name)
    per_100k_in_state is population/state_population*100
    per_100k_with_name is exclude(population,state)/exclude(population,name,state)*100
    state_popularity is per_100k_in_state/per_100k_with_name


  query: iconic_state is {
    group_by: state
    // if a field ends with the name 'list', it is displayed a a list of values
    nest: iconic_names_list is {
      group_by: name
      aggregate: state_popularity
      // total_name_populution
      // state_population
      limit: 20
      having: population > 1000
    }
    limit: 50
  }

  query: iconic_state_female is iconic_state {? gender='F'}
  query: iconic_state_male is iconic_state {? gender='M'}
  query: iconic_recent is iconic_state {? decade=1990}

  measure: 
    total_name_populution2 is exclude(population, state)
    decade_population is exclude(population, name)
    per_100k_in_decade is population/decade_population*100
    per_100k_with_name2 is exclude(population,state)/exclude(population,name,decade)*100
    decade_popularity is per_100k_in_decade/per_100k_with_name2
  
  query: iconic_decade is {
    group_by: decade
    // if a field ends with the name 'list', it is displayed a a list of values
    nest: iconic_names_list is {
      group_by: name
      aggregate: decade_popularity
      // total_name_populution
      // state_population
      limit: 30
      having: population > 1000
    }
    limit: 50
  }

  query: iconic_decade_female is iconic_decade {? gender='F'}
  query: iconic_decade_male is iconic_decade {? gender='M'}

  query: iconic_decade_state is {
    group_by: state
    nest: iconic_decade
  }
}